# Loathsxome plugin: filtertag
# Abhijit Menon-Sen <ams@toroid.org>
#
# Parses tag filter expressions in PATH_INFO, and restricts the view to
# posts matching the specified tag(s).
#
# Template variables:
#
# $rsslink is a link to the most relevant RSS feed for a set of results.
# $taglinks is a string containing zero or more links to the individual
# tags for a post.

package loathsxome::filtertag;

sub start {
    1;
}

sub parse_uri {
    return 1 if $loathsxome::post;

    # In parsing the tag list, we have to consider the following cases
    # (with or without trailing slashes):
    #
    # a         (a)     (common case)
    # a+b       (a b)   (not unlikely)
    # a/b       (a/b)   (used by autotag)
    #
    # We take a conservative approach that just happens to permit c++ as
    # a valid tag name (as some people who shall remain nameless seem to
    # expect). But don't try to use a tag named "a+b". Just Don't Do It.

    if ($loathsxome::path_info =~ s/^((?:[\w\d-]+\+)+[\w\d-]+)(?:\/|$)//) {
        push @{$loathsxome::filters{tags}}, split(/\+/, $1);
    }
    elsif ($loathsxome::path_info =~ s/^((?:[\w\d-]+\/)+[\w\d-]+)(?:\/|$)// ||
           $loathsxome::path_info =~ s/^([\w\d+-]+)(?:\/|$)//)
    {
        push @{$loathsxome::filters{tags}}, $1;
    }
}

sub filter {
    my %want;
    if (exists $loathsxome::filters{tags}) {
        %want = map { $_ => 1 } @{$loathsxome::filters{tags}};
    }

    foreach my $entry (keys %loathsxome::entries) {
        my %have = map { $_ => 1 } @{$loathsxome::entries{$entry}{tags}};

        foreach my $tag (keys %want) {
            delete $loathsxome::entries{$entry}
                unless exists $have{$tag};
        }
    }
}

sub head {
    my ($pkg, $post, $head) = @_;

    if (exists $loathsxome::filters{tags}) {
        my $tags = join '+', @{$loathsxome::filters{tags}};
        $loathsxome::rsslink = qq{$loathsxome::url/$tags/index.rss};
    }

    return 1;
}

sub story {
    my ($pkg, $entry, $fn, $story, $title, $body) = @_;

    my @links;
    foreach my $tag (@{$loathsxome::entries{$entry}{tags}}) {
        push @links, qq{<a href="$loathsxome::url/$tag">$tag</a>};
    }

    $loathsxome::taglinks = "";
    if (@links) {
        $loathsxome::taglinks = join ', ', @links;
    }
}

1;
